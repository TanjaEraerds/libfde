#ifndef __FORTRES_DIRENT_FMOD
#define __FORTRES_DIRENT_FMOD

#include "fortres/itfUtil.fpp"

      module dirent
        use stringref
        use, intrinsic :: iso_c_binding
        implicit none
        private

        type, bind(C) :: DIR_t
          private
          type(c_ptr) :: ptr
        end type

        public :: DIR_t
        public :: relPath_from_to, relPath_to
        public :: opendir, readdir, closedir, rewinddir

        interface relPath
          module procedure relPath_from_to, relPath_to
        end interface

        interface
          subroutine mkrp_ft_( r, s, t ) _cID(f_make_relPath_from_to)
            import StringRef_t
            type(StringRef_t) :: r    !< result
            type(StringRef_t) :: s, t !< paths src + tgt
          end subroutine

          subroutine mkrp_t_( r, t ) _cID(f_make_relPath_to)
            import StringRef_t
            type(StringRef_t) :: r !< result
            type(StringRef_t) :: t !< tgt-path
          end subroutine

          function opendir_( dir, path ) _cID(f_opendir)
            import DIR_t, StringRef_t, c_int
            type(DIR_t),      intent(out) :: dir
            type(StringRef_t), intent(in) :: path
            integer(kind=c_int)           :: opendir_
          end function

          function readdir_( dir, dirEntry ) _cID(f_readdir)
            import DIR_t, StringRef_t, c_int
            type(DIR_t),  value :: dir
            type(StringRef_t)   :: dirEntry
            integer(kind=c_int) :: readdir_
          end function

          subroutine closedir_( dir, stat ) _cID(f_closedir)
            import DIR_t, c_int
            type(DIR_t), intent(inout) :: dir
            integer(kind=c_int)        :: stat
          end subroutine
        
          subroutine rewinddir_( dir, stat ) _cID(f_rewinddir)
            import DIR_t, c_int
            type(DIR_t),  value :: dir
            integer(kind=c_int) :: stat
          end subroutine
        end interface

      contains

          subroutine relPath_from_to( res, from, to )
            character(len=*), intent(inout) :: res
            character(len=*), intent(in)    :: from, to
            call mkrp_ft_( str(res), str(from), str(to) )
          end subroutine
      
          subroutine relPath_to( res, to )
            character(len=*), intent(inout) :: res
            character(len=*), intent(in)    :: to
            call mkrp_t_( str(res), str(to) )
          end subroutine
      
          function opendir( dir, path )
            type(DIR_t),     intent(out) :: dir
            character(len=*), intent(in) :: path
            logical                      :: opendir
            opendir = (opendir_( dir, str(path) ) /= 0)
          end function
        
          function readdir( dir, dirEntry )
            type(DIR_t),               value :: dir
            type(StringRef_t), intent(inout) :: dirEntry
            logical                          :: readdir
            readdir = (readdir_( dir, dirEntry ) /= 0)
          end function

          subroutine closedir( dir, stat )
            type(DIR_t), intent(inout) :: dir
            logical,          optional :: stat
            integer(kind=c_int)        :: stat_
            call closedir_( dir, stat_ )
            if (present(stat)) stat = stat_
          end subroutine
  
          subroutine rewinddir( dir, stat )
            type(DIR_t),  value :: dir
            logical,   optional :: stat
            integer(kind=c_int) :: stat_
            call rewinddir_( dir, stat_ )
            if (present(stat)) stat = stat_
          end subroutine
      end module
#endif

